; array8.L1
; Reverse an array

(:main
  (:main
    0 1
    (rdi <- 11)
    (rsi <- 7)
    (call allocate 2)
    ((mem rax 8) <- 3)
    ((mem rax 16) <- 5)
    ((mem rax 24) <- 7)
    ((mem rax 32) <- 9)
    ((mem rax 40) <- 11)
    ((mem rsp -8) <- :reverse_ret)
    (rdi <- rax)
    (call :reverse 1)
    :reverse_ret
    (rdi <- rax) 
    (call print 1)
    (return))
  (:swap_pair
    2 0
    (r10 <- (mem rdi 0))
    (r11 <- (mem rsi 0))
    ((mem rdi 0) <- r11)
    ((mem rsi 0) <- r10)
    (return)) 
  (:reverse
    1 3
    (r8 <- rdi)
    (r8 += 8)
    (r9 <- (mem rdi 0))
    (r9 <<= 3)
    (r9 += rdi)
    ((mem rsp 16) <- rdi) ; for return later
    :for_start
    (cjump r8 <= r9 :for_body :for_end)
    :for_body
    ((mem rsp -8) <- :swap_ret)
    (rdi <- r8)
    (rsi <- r9)
    ((mem rsp 0) <- r8)
    ((mem rsp 8) <- r9)
    (call :swap_pair 2) 
    :swap_ret
    (r8 <- (mem rsp 0))
    (r9 <- (mem rsp 8))
    (r8 += 8)
    (r9 -= 8)
    (goto :for_start)
    :for_end
    (rax <- (mem rsp 16))
    (return))
)


